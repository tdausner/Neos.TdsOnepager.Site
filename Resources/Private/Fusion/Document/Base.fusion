prototype(TdsOnepager.Site:Document.Base) < prototype(Neos.Fusion:Component) {

    @context.inWrapper = ${q(documentNode).is('[instanceof TdsOnepager.Site:Document.Wrapper]')}
    title = ${q(documentNode).property('title')}
    titleOverride = ${q(documentNode).property('titleOverride')}

    renderer = Neos.Neos:Page {

        prototype(Neos.Fusion:Http.ResponseHead) {
            statusCode = ${node.context.inBackend || !inWrapper ? 200 : 404}
        }

        bodyTag.attributes.class = 'cc-bgw'
        # fix for the safari iphone/ipad css hover bug
        bodyTag.attributes.onclick = ''

        head {
            metaViewport = '<meta name="viewport" content="width=device-width">'
            favicon = TdsOnepager.Site:Resource.FaviconInclude {
                iconType = 'img/png'
                iconSize = '50x50'
                path = 'resource://TdsOnepager.Site/Public/Assets/Favicon/square.png'
            }

            stylesheets = TdsOnepager.Site:Resource.StyleSheets
            openSans  = TdsOnepager.Site:Resource.FontInclude {
                path = 'resource://TdsOnepager.Site/Public/Fonts/OpenSans-Regular.ttf'
            }
            fontAwesome  = TdsOnepager.Site:Resource.FontInclude {
                path = 'resource://TdsOnepager.Site/Public/Fonts/fa-light-300.woff2'
            }
            titleTag {
                content = ${props.titleOverride ? props.titleOverride : props.title}
            }
        }

        body = ${props.body}

        bodyScripts = TdsOnepager.Site:Resource.BodyScripts
        bodyScripts.@position = 'before closingBodyTag'
    }
}

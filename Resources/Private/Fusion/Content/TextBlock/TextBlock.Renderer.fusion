prototype(TdsOnepager.Site:Content.TextBlock.Renderer) < prototype(Neos.Fusion:Renderer) {

    @context {
        spanClass = Neos.Fusion:Case {
            hasSpan {
                condition = ${props.hasSpan && !node.context.inBackend}
                renderer = ${"author col-12" + String.pregReplace(props.author, '/.*<span style="text-align:(.*);">.*<\/span>.*/', ' text-block--$1')}
            }
            default {
                condition = true
                renderer = "author col-12"
            }
        }
        sound = Neos.Fusion:Tag {
            tagName = 'audio'
            attributes.src = Neos.Fusion:ResourceUri {
                resource = ${props.sound.resource}
            }

            @if.notEmpty = ${props.sound}
        }
        icon = Neos.Fusion:Tag {
            tagName = "p"
            attributes.class = "text-block__icon d-none d-md-flex"
            content = ${'<i class="fa fa-' + props.icon + '">' + sound + '</i>'}
        }
        icon.@if.notEmpty = ${props.icon}
    }

    renderer = TdsOnepager.Site:Resource.Section {

        className = ${"text-block text-block__author--" + props.authorPosition + " " + props.colors}
        animation = ${props.animation}
        padding = ${props.padding}

        content = Neos.Fusion:Array {

            title = afx`
                <div class="col-12">
                    <h2>{props.title}</h2>
                </div>
            `
            title.@if.notEmpty = ${props.title || props.inBackend}

            authorTop = afx`
                <div class={spanClass}>
                    <h4>&nbsp;{props.author}&nbsp;</h4>
                </div>
            `
            authorTop.@if.notEmpty = ${(props.author || props.inBackend) && props.authorPosition == "top"}

            text = Neos.Fusion:Tag {
                attributes.class = ${"col-12 text-block--" + props.textColumns}
                content = ${props.text}
            }

            authorBottom = afx`
                <div class={spanClass}>
                    <h4>&nbsp;{props.author}&nbsp;</h4>
                </div>
            `
            authorBottom.@if.notEmptyAndBottom = ${(props.author || props.inBackend) && props.authorPosition == 'bottom'}
        }
    }
}

prototype(TdsOnepager.Site:Resource.Section.Renderer) < prototype(Neos.Fusion:Renderer) {

    @context.row = Neos.Fusion:Array {
        permlink = afx`
            <div class="perm-link">
                <a href={props.permalink}><i class="fa fa-link"></i></a>
            </div>
        `
        permlink.@if.doIt = ${!props.noID && !props.noLink}
        content = Neos.Fusion:Tag {
            attributes.class = "row"
            content = ${props.content}
        }
    }

    renderer = Neos.Fusion:Tag {

        tagName = "section"
        attributes.id = ${props.id}
        attributes.id.@if.doIt = ${!props.noID}
        attributes.class = ${props.className}
        content = Neos.Fusion:Tag {

            attributes.class = "container"
            content = Neos.Fusion:Case {

                animated {
                    condition = ${props.animation ? 1 : 0}
                    renderer = Neos.Fusion:Tag {

                        attributes.class = ${"animate animate__animated animate__" + props.animation}
                        attributes.data-animation = ${props.animation}
                        content = ${row}
                    }
                }

                notAnimated {
                    condition = true
                    renderer = ${row}
                }
            }
        }
    }

}
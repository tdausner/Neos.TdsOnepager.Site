prototype(TdsOnepager.Site:Resource.Section.Renderer) < prototype(Neos.Fusion:Renderer) {

    @context.row = Neos.Fusion:Array {
        permlink = afx`
            <div class="perm-link">
                <a href={props.permalink}><i class="fa fa-link"></i></a>
            </div>
        `
        permlink.@if {
            linkit = ${!props.noLink}
            paddingOK = ${props.padding ? (props.padding != "lower" && props.padding != "none") : true}
        }
        content = Neos.Fusion:Case {

            noRow {
                condition = ${props.noRow}
                renderer = ${props.content}
            }

            hasRow {
                condition = ${!props.noRow}
                renderer = Neos.Fusion:Tag {
                    attributes.class = "row"
                    content = ${props.content}
                }
            }
        }
    }

    renderer = Neos.Fusion:Tag {

        tagName = "section"
        attributes {
            id = ${props.id}
            id.@if.doIt = ${!props.noID}
            class = ${props.className + (props.padding ? (" section--padding-" + props.padding) : "")}
        }
        content = Neos.Fusion:Tag {

            attributes.class = "container"
            content = Neos.Fusion:Case {

                animated {
                    condition = ${props.animation ? 1 : 0}
                    renderer = Neos.Fusion:Tag {

                        attributes.class = ${"animate animate__animated animate__" + props.animation}
                        attributes.data-animation = ${props.animation}
                        content = ${row}
                    }
                }

                notAnimated {
                    condition = true
                    renderer = ${row}
                }
            }
        }
    }

}